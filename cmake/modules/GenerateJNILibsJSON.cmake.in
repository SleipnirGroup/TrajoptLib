set(JAVA_LIB_TARGET_PATH "@JAVA_LIB_TARGET_PATH@")

message(STATUS "Java lib target path: ${JAVA_LIB_TARGET_PATH}")

file(GLOB lib_files "${JAVA_LIB_TARGET_PATH}/*")
list(FILTER lib_files EXCLUDE REGEX ".*TrajoptLib-java.*")

set(json_string "{}")

string(JSON json_string SET ${json_string} deps "[]")

list(LENGTH lib_files lib_files_length)
math(EXPR lib_files_upper_limit "${lib_files_length} - 1")

foreach(i RANGE ${lib_files_upper_limit})
  list(GET lib_files ${i} lib_file)
  get_filename_component(file_name ${lib_file} NAME)
  file(MD5 ${lib_file} hash)
  string(JSON json_string SET ${json_string} deps ${i} "{}")
  string(JSON json_string SET ${json_string} deps ${i} name \"${file_name}\")
  string(JSON json_string SET ${json_string} deps ${i} hash \"${hash}\")
endforeach()

file(GLOB jni_file "${JAVA_LIB_TARGET_PATH}/*TrajoptLib-java*")
get_filename_component(jni_file_name ${jni_file} NAME)
file(MD5 ${jni_file} jni_hash)
string(JSON json_string SET ${json_string} jni "{}")
string(JSON json_string SET ${json_string} jni name \"${jni_file_name}\")
string(JSON json_string SET ${json_string} jni hash \"${jni_hash}\")

set(file_path "${JAVA_LIB_TARGET_PATH}/native_libs.json")

file(WRITE ${file_path} "${json_string}")
